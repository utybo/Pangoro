plugins {
    id 'org.jetbrains.kotlin.multiplatform' version '1.3.72'
    id "maven-publish"
    id "jacoco"
}

group 'guru.zoroark'
version '0.1-SNAPSHOT'

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

kotlin {
    jvm()
    js {
        browser {
        }
        nodejs {
        }
    }

    sourceSets {
        commonMain {
            dependencies {
                implementation kotlin('stdlib')
                api 'guru.zoroark.lixy:lixy:5313645728'
                implementation kotlin("reflect")
            }
        }
        commonTest {
            dependencies {
                implementation kotlin('test-common')
                implementation kotlin('test-annotations-common')
            }
        }
        jvmMain {
            dependencies {
                implementation kotlin('stdlib-jdk8')
            }
        }
        jvmTest {
            dependencies {
                implementation kotlin('test-junit')
            }
        }
        jsMain {
            dependencies {
                implementation kotlin('stdlib-js')
            }
        }
        jsTest {
            dependencies {
                implementation kotlin('test-js')
            }
        }
    }
}

task jacocoTestReport(type: JacocoReport) {
    executionData jvmTest
    reports {
        xml.enabled = true
    }
    classDirectories.setFrom(fileTree("${buildDir}/classes/kotlin/jvm/main"))
    sourceDirectories.setFrom(
            kotlin.sourceSets["commonMain"].kotlin.sourceDirectories,
            kotlin.sourceSets["jvmMain"].kotlin.sourceDirectories
    )
}